//===== EinherjarRO Scripts ================================== 
//= WeaponMasteries
//===== By: ================================================== 
//= Stolao
//===== Current Version: ===================================== 
//= 1.26
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= As you train using a particulat weapon type you get passive 
//= buffs depending on how much yo have used that type
//= There are five types Slash, Stab, Smash, Ranged and Unique
//===== Comments: ============================================
//= Masteries can go up to level 9999 with curve of 21
//= Requires OnEvent/OnKillNPCEvent.txt to function
//===== Additional Comments: =================================
//= 1.00 Origioanal Make
//= 1.01 +33% Mastery >> Atk
//= 1.02 +50% Mastery >> Matk
//= 1.03 Rename Masteries
//= 1.04 Redistibute Weapons into new masteries
//= 1.05 Wiped Mastery Bonuses
//= 1.06 Added Starting Bonuses
//= 1.07 Made OnPCStatCalcEvent for Mastery System
//= 1.08 Added mastery Bonueses for Lvl 1, 2, 4, 8, 16, 32 ,64, and 128
//= 1.09 Changed Mysticals MaxSPRate for MaxSp
//= 1.0A Added @wi (see bindatcmd.txt)
//= 1.0B Added @wlvl (see bindatcmd.txt)
//= 1.0C Removed dependency from bindatcmd.txt
//= 1.0D Rebalanced a few formulas for differant masteries
//= 1.0E Combined with old Sect System
//= 1.0F Added Sect give +10 total on varried stats
//= 1.10 Added 10% Resistance to sects Element
//= 1.11 Added 10% more damage with sects element
//= 1.12 Changed Sect Stats from +10 -> +3
//= 1.13 Changed Wm Stat bonus from M/9 -> M/32
//= 1.14 Removed Wm Booster item
//= 1.15 Lowered Wm Max Level to 99
//= 1.16 Rewrote Onslaught Formulas for lower effect
//= 1.17 Rewrote Switfness Formulas for lower effect
//= 1.18 Rewrote Safegaurd Formulas for lower effect
//= 1.19 Rewrote Ranged Formulas for lower effect
//= 1.1A Rewrote Mystical Formulas for lower effect
//= 1.1B Added Exprates = job_exp_rate
//= 1.1C Changed Exp Curve to reflect new Exp Rates
//= 1.1D Added Debugmes on Overflow
//= 1.1E Added Increased Exp from SC_JEXPBOOST,2
//= 1.1F Fixed Spacing/ Readability in OnInit
//= 1.20 Fixed Typos
//= 1.21 Fixed Bug with No gear On
//= 1.22 Fixed Missing )
//= 1.23 Convert to new formatting to allow both hand to recive buffs
//= 1.24 Added a .Left Config
//= 1.25 Added a Small boost to secondary stat based off prime stat
//= 1.26 Added a Check for Item in Hand + Conf 
//= 1.27 Fixed mssing "
//= 1.28 Fixed missing )
//= 1.29 Added a missing conf setting
//===== Contact Ifo: =========================================
//= [Stolao] 
//= Email: Taingram11@gmail.com
//============================================================
//
// Str - Onslaught:	2HSword,1HAxe,2HAxe,1HMace,2HMace(unused),Knuckle
// Agi - Swiftness:	Dagger,Fuuma Shuriken,Katar
// Vit - Safegaurd:	Unarmmed,1HSword,1HSpear,2HSpear,Specialty
// Dex - Ranged:	Bow,Instrument,Whip,Revolver,Rifle,Gatling Gun,Shotgun,Grenade Launcher
// Int - Mystical:	Rod,Book,2HStaff,Scythe

function	script	Onslaught_M	{
	if(getarg(0,0) >= 1)	bonus bCritical,1+getarg(0,0)/20;
	if(getarg(0,0) >= 2)	bonus bAtk,1+getarg(0,0)/4;
	if(getarg(0,0) >= 4)	bonus bAtk2,1+getarg(0,0)/4;
	if(getarg(0,0) >= 8)	bonus bPerfectHitAddRate,1+getarg(0,0)/21;
	if(getarg(0,0) >= 16)	bonus bBaseAtk,getarg(0,0)/4+1;
	if(getarg(0,0) >= 32)	bonus bCritAtkRate,getarg(0,0)/4+1;
	if(getarg(0,0) >= 64)	bonus bAspdRate,getarg(0,0)/22 + 1;
	if(getarg(0,0) >= 128)	bonus bSplashAddRange,1;
	bonus bStr,1+getarg(0,0)/32;
	bonus bAtkrate,1+getarg(0,0)/7+((1+getarg(0,0)/7)*readparam(bStr)/100);	
return;
}

function	script	Swiftness_M	{
	if(getarg(0,0) >= 1)	bonus bSpeedAddRate,1+getarg(0,0)/9;
	if(getarg(0,0) >= 2)	bonus bFlee,getarg(0,0)/3;
	if(getarg(0,0) >= 4)	bonus bDoubleAddRate,1+getarg(0,0)/9;
	if(getarg(0,0) >= 8)	bonus bFlee2,1+getarg(0,0)/33;
	if(getarg(0,0) >= 16)	bonus bPerfectHitAddRate,1+getarg(0,0)/11;
	if(getarg(0,0) >= 32)	bonus bCritAtkRate,getarg(0,0)/9-1;
	if(getarg(0,0) >= 64)	bonus bCritical,1+getarg(0,0)/20;
	if(getarg(0,0) >= 128)	bonus2 bHPDrainRate,1+getarg(0,0),1+getarg(0,0)/24;
	bonus bAgi,1+getarg(0,0)/32;
	bonus bAspdRate,1+getarg(0,0)/7+((1+getarg(0,0)/7)*readparam(bAgi)/100);
return;
}

function	script	Safegaurd_M	{
	if(getarg(0,0) >= 1)	bonus bHPrecovRate,1+getarg(0,0)/2;
	if(getarg(0,0) >= 2)	bonus bDef,1+getarg(0,0)/20;
	if(getarg(0,0) >= 4)	bonus bMdef,1+getarg(0,0)/24;
	if(getarg(0,0) >= 8)	bonus bMaxHPrate,1+getarg(0,0)/11;
	if(getarg(0,0) >= 16)	bonus bNearAtkDef,getarg(0,0)/20+1;
	if(getarg(0,0) >= 32)	bonus bLongAtkDef,getarg(0,0)/21+1;
	if(getarg(0,0) >= 64)	bonus bMagicAtkDef,getarg(0,0)/22+1;
	if(getarg(0,0) >= 128)	bonus bCriticalDef,getarg(0,0)/23+1;
	bonus bVit,1+getarg(0,0)/32;
	bonus bMaxHP,getarg(0,0)*5+5+((getarg(0,0)*5+5)*readparam(bVit)/100);
return;
}

function	script	Ranged_M	{
	if(getarg(0,0) >= 1)	bonus bBaseAtk,getarg(0,0)/3+2;
	if(getarg(0,0) >= 2)	bonus bCritical,1+getarg(0,0)/21;
	if(getarg(0,0) >= 4)	bonus bFlee,1+getarg(0,0)/11;
	if(getarg(0,0) >= 8)	bonus bAspd,getarg(0,0)/11;
	if(getarg(0,0) >= 16)	bonus bHit,getarg(0,0)/9;
	if(getarg(0,0) >= 32)	bonus bLongAtkRate,getarg(0,0)/9-1;
	if(getarg(0,0) >= 64)	bonus bCritAtkRate,4+getarg(0,0)/9;
	if(getarg(0,0) >= 128)	bonus bAtkRange,1+getarg(0,0)/99;
	bonus bDex,1+getarg(0,0)/32;
	bonus bAtkrate,1+getarg(0,0)/7+((1+getarg(0,0)/7)*readparam(bDex)/100);
return;
}

function	script	Mystical_M	{
	if(getarg(0,0) >= 1)	bonus bSPrecovRate,1+getarg(0,0)/2;
	if(getarg(0,0) >= 2)	bonus bMaxSP,getarg(0,0)*2+2;
	if(getarg(0,0) >= 4)	bonus bCastrate,-1-getarg(0,0)/20;
	if(getarg(0,0) >= 8)	bonus bUseSPrate,-1-getarg(0,0)/21;
	if(getarg(0,0) >= 16)	bonus bDelayrate,-1-getarg(0,0)/22;
	if(getarg(0,0) >= 32)	bonus bHealPower,1+getarg(0,0)/2;
	if(getarg(0,0) >= 64)	bonus bMAtkrate,1+getarg(0,0)/7;
	if(getarg(0,0) >= 128)	bonus bNoCastCancel,1;
	bonus bInt,1+getarg(0,0)/32;
	bonus bEquipmentMatk,1+getarg(0,0)+((1+getarg(0,0))*readparam(bInt)/100);
	
return;
}

prontera,5,5,5	script	PCSTATCALC	111,{
end;
OnWeaponInfo:
	set .@M$,.@atcmd_parameters$[0];
	if((.@M$ == "")){
		switch(getiteminfo(getequipid(EQI_HAND_R),11)){
			case 3:  	case 6: 	case 7: 	case 8:	
			case 9:		case 12:					set .@M$,"Onslaught";	Break;
			case 1: 	case 16:	case 22:			set .@M$,"Swiftness";	Break;
			case 2: 	case 4: 	case 5:		default:	set .@M$,"Safegaurd";	Break;
			case 11:	case 13:	case 14:	case 17:
			case 18:	case 19:	case 20:	case 21:	set .@M$,"Ranged";	Break;
			case 10:	case 15:	case 23:	case 24:	set .@M$,"Mystical";	Break;
		}
	}
	if(.@M$ == "Onslaught" || .@M$ == "onslaught" || .@M$ == "OnSlaught") set .@i,1;
	if(.@M$ == "Swiftness" || .@M$ == "swiftness") set .@i,2;
	if(.@M$ == "Safegaurd" || .@M$ == "SafeGaurd" || .@M$ == "safegaurd") set .@i,3;
	if(.@M$ == "Ranged"    || .@M$ == "ranged"   ) set .@i,4;
	if(.@M$ == "Mystical"  || .@M$ == "mystical" ) set .@i,5;
	set .@X,atoi(.@atcmd_parameters$[1]);
	if(!.@X) set .@X,getd(""+.@M$+"Master");
	switch(.@i){
		case 1:
			dispbottom "Onslaught Lv: "+.@X+" "+OnslaughtExp+" / "+((OnslaughtMaster*OnslaughtMaster*2500)+10+OnslaughtMaster)+"xp";
			dispbottom "Str +"+(1+.@X/32)+"";
			dispbottom "Atk +"+(1+.@X/9)+"%";
			if(.@X >= 1)dispbottom "Critical Chance +"+(1+.@X/20)+"";
			if(.@X >= 2)dispbottom "Atk +"+(1+.@X/9)+"";
			if(.@X >= 4)dispbottom "Weapon Atk +"+(1+.@X/11)+"";
			if(.@X >= 8)dispbottom "Perfect Hit +"+(1+.@X/21)+"";
			if(.@X >= 16)dispbottom "Base Atk +"+(.@X/4+1)+"";
			if(.@X >= 32)dispbottom "Crit Damage +"+(.@X/8+3)+"%";
			if(.@X >= 64)dispbottom "Aspd +"+.@X/16+"%";
			if(.@X >= 128)dispbottom "Splash attack radius +1";
		break;
		case 2:	
			dispbottom "Swiftness Lv: "+.@X+" "+SwiftnessExp+" / "+((SwiftnessMaster*SwiftnessMaster*2500)+10+SwiftnessMaster)+"xp";
			dispbottom "Agi +"+(1+.@X/32)+"";
			dispbottom "Aspd +"+(1+.@X/9)+"%";
			if(.@X >= 1)dispbottom "Moving speed +"+(1+.@X/9)+"%";
			if(.@X >= 2)dispbottom "Flee +"+(.@X/2)+"";
			if(.@X >= 4)dispbottom "Double Attack Chance +"+(1+.@X/9)+"%";
			if(.@X >= 8)dispbottom "Perfect Dodge +"+(1+.@X/33)+"";
			if(.@X >= 16)dispbottom "On-target impact chance +"+(1+.@X/11)+"%";
			if(.@X >= 32)dispbottom ""+(1+SwiftnessMaster)/10+"."+((1+SwiftnessMaster)%10)+"% chance of healing for "+(.@X/24+1)+"% of damage dealt with each normal attack";
			if(.@X >= 64)dispbottom "Critical Chance +"+(1+.@X/20)+"";
			if(.@X >= 128)dispbottom "Crit Damage +"+(.@X/9-1)+"%";
		break;
		case 3:
			dispbottom "Safegaurd Lv: "+.@X+" "+SafegaurdExp+" / "+((SafegaurdMaster*SafegaurdMaster*2500)+10+SafegaurdMaster)+"xp";
			dispbottom "Vit +"+(1+.@X/32)+"";
			dispbottom "Max HP +"+(.@X*5+5)+"";
			if(.@X >= 1)dispbottom "Natural HP recovery ratio +"+(1+.@X/2)+"%";
			if(.@X >= 2)dispbottom "Def +"+(1+.@X/20)+"";
			if(.@X >= 4)dispbottom "Mdef +"+(1+.@X/24)+"";
			if(.@X >= 8)dispbottom "Max HP +"+(1+.@X/9)+"%";
			if(.@X >= 16)dispbottom "Adds "+(.@X/20+1)+" damage reduction against Critical Hits";
			if(.@X >= 32)dispbottom "Adds "+(.@X/21+1)+" damage reduction against melee physical attacks";
			if(.@X >= 64)dispbottom "Adds "+(.@X/22+1)+" damage reduction against ranged physical";
			if(.@X >= 128)dispbottom "Adds "+(.@X/23+1)+" damage reduction against magical attacks";
		break;
		case 4:
			dispbottom "Ranged Lv: "+.@X+" "+RangedExp+" / "+((RangedMaster*RangedMaster*2500)+10+RangedMaster)+"xp";
			dispbottom "Dex +"+(1+.@X/32)+"";
			dispbottom "Atk +"+(1+.@X/9)+"%";
			if(.@X >= 1)dispbottom "Moving speed +"+(1+.@X/20)+"%";
			if(.@X >= 2)dispbottom "Critical +"+(1+.@X/21)+"";
			if(.@X >= 4)dispbottom "Flee +"+(1+.@X/2)+"";
			if(.@X >= 8)dispbottom "Aspd +"+(.@X/11)+"";
			if(.@X >= 16)dispbottom "Hit +"+(.@X/9)+"";
			if(.@X >= 32)dispbottom "Increases damage of ranged attacks by "+(.@X/11)+"%";
			if(.@X >= 64)dispbottom "Crit Damage +"+(4+.@X/9)+"%";
			if(.@X >= 128)dispbottom "Atk Range +"+(1+.@X/99)+"";
		break;
		case 5:
			dispbottom "Mystical Lv: "+.@X+" "+MysticalExp+" / "+((MysticalMaster*MysticalMaster*2500)+10+MysticalMaster)+"xp";
			dispbottom "Int +"+(1+.@X/32)+"";
			dispbottom "Matk +"+(1+.@X/9)+"%";
			if(.@X >= 1)dispbottom "Natural SP recovery ratio +"+(.@X/2+1)+"%";
			if(.@X >= 2)dispbottom "Max SP +"+(2+.@X*2)+"";
			if(.@X >= 4)dispbottom "Skill casting time "+(-1-.@X/20)+"%";
			if(.@X >= 8)dispbottom "SP consumption "+(-1-.@X/21)+"%";
			if(.@X >= 16)dispbottom "Decreases skill delay by "+(1+.@X/22)+"%";
			if(.@X >= 32)dispbottom "Increase heal amount of all heal skills by "+(1+.@X/2)+"%";
			if(.@X >= 64)dispbottom "Weapon magical attack power +"+(.@X+1)+"";
			if(.@X >= 128)dispbottom "Prevents casting from being interrupted when hit";
		break;
		default:
			dispbottom "@wi <Type> <Level>";
			dispbottom "Type: Onslaught, Swiftness, Safegaurd, Ranged, Mystical";
		break;
	}
end;
OnWeaponLevel:
	set .@M$,.@atcmd_parameters$[0];
	set .@X,atoi(.@atcmd_parameters$[1]);
	if((.@M$ != "Onslaught" && .@M$ != "Swiftness" && .@M$ != "Safegaurd" && .@M$ != "Ranged" && .@M$ != "Mystical" && .@M$ != "All")||!.@X){
		dispbottom "@wlvl <type> <lvl>";
		dispbottom "Onslaught,  Swiftness,  Safegaurd,  Ranged,  Mystical,  All";
		end;
	}
	if(getvariableofnpc(.MaxMastery,"NPCKILL") >= .@X){
		if(.@M$ == "All"){
			setarray .@X$[0],"Onslaught","Swiftness","Safegaurd","Ranged","Mystical";
			set .@i,0;
			while(.@i < 5){
				setd ""+.@X$[.@i]+"Master",.@X;
				setd ""+.@X$[.@i]+"Exp",0;
				dispbottom ""+.@X$[.@i]+" Mastery has advanced to Lv "+.@X+"!";
				set .@i,.@i+1;
			}
		} else {
			setd ""+.@M$+"Master",.@X;
			setd ""+.@M$+"Exp",0;
			dispbottom ""+.@M$+" Mastery has advanced to Lv "+.@X+"!";
		}
	} else {
		dispbottom "Sorry. Your "+.@M$+" Mastery has reached its limit!";
	}
end;

OnPCStatCalcEvent:
	if(.Weapon){
		if(getequipid(EQI_HAND_R) || (!getequipid(EQI_HAND_R) && .RightFist)){
			switch(getiteminfo(getequipid(EQI_HAND_R),11)){
				case 3:  	case 6: 	case 7: 	case 8:	
				case 9:		case 12:					Onslaught_M(OnslaughtMaster);	Break;
				case 1: 	case 16:	case 22:			Swiftness_M(SwiftnessMaster);	Break;
				case 2: 	case 4: 	case 5:		default:	Safegaurd_M(SafegaurdMaster);	Break;
				case 11:	case 13:	case 14:	case 17:
				case 18:	case 19:	case 20:	case 21:	Ranged_M(RangedMaster);		Break;
				case 10:	case 15:	case 23:	case 24:	Mystical_M(MysticalMaster);	Break;
			}
		}
		if(getequipid(EQI_HAND_L) && getiteminfo(getequipid(EQI_HAND_R),5) != 34 && .Left){
			if((!getequipid(EQI_HAND_L) || !.LeftFist)) end;
			switch(getiteminfo(getequipid(EQI_HAND_L),11)){
				case 3:  	case 6: 	case 7: 	case 8:	
				case 9:		case 12:					Onslaught_M(OnslaughtMaster/2);	Break;
				case 1: 	case 16:	case 22:			Swiftness_M(SwiftnessMaster/2);	Break;
				case 2: 	case 4: 	case 5:		default:	Safegaurd_M(SafegaurdMaster/2);	Break;
				case 11:	case 13:	case 14:	case 17:
				case 18:	case 19:	case 20:	case 21:	Ranged_M(RangedMaster/2);		Break;
				case 10:	case 15:	case 23:	case 24:	Mystical_M(MysticalMaster/2);	Break;
			}
		}
	}
end;

OnNPCKillEvent:
	if(.Weapon){
		set .@M$,"";
		if(getequipid(EQI_HAND_R) || (!getequipid(EQI_HAND_R) && .RightFist)){
			switch(getiteminfo(getequipid(EQI_HAND_R),11)){
				case 3:  	case 6: 	case 7: 	case 8:	
				case 9:		case 12:					Set .@M$,"Onslaught";	Break;
				case 1: 	case 16:	case 22:			Set .@M$,"Swiftness";	Break;
				case 2: 	case 4: 	case 5:		default:	Set .@M$,"Safegaurd";	Break;
				case 11:	case 13:	case 14:	case 17:
				case 18:	case 19:	case 20:	case 21:	Set .@M$,"Ranged";	Break;
				case 10:	case 15:	case 23:	case 24:	Set .@M$,"Mystical";	Break;
			}
			if(.MaxMastery > getd(""+ .@M$ + "Master")){
				Setd ""+.@M$+"Exp",getd(""+.@M$+"Exp") + (strmobinfo(3,killedrid)+.wLvBonus*getiteminfo(getequipid(EQI_HAND_R),13)) * (getbattleflag("job_exp_rate") + getbattleflag("job_exp_rate") * getstatus(SC_JEXPBOOST,2));
				if(getd(""+.@M$+"Exp") > getd(""+.@M$+"Master")*(getd(""+.@M$+"Master")*.expcurve)+10+getd(""+.@M$+"Master")){
					setd ""+.@M$+"Master",getd(""+.@M$+"Master")+1;
					announce ""+.@M$+" Mastery has advanced to Lv "+getd(""+.@M$+"Master")+"!",bc_blue|bc_self;
					Setd ""+.@M$+"Exp",0;
				}
			}
		}
		if(getequipid(EQI_HAND_L) && getiteminfo(getequipid(EQI_HAND_R),5) != 34 && .Left){
			if((!getequipid(EQI_HAND_L) && !.LeftFist)) end;
			set .@M$,"";
			switch(getiteminfo(getequipid(EQI_HAND_L),11)){
				case 3:  	case 6: 	case 7: 	case 8:	
				case 9:		case 12:					Set .@M$,"Onslaught";	Break;
				case 1:		case 16:	case 22:			Set .@M$,"Swiftness";	Break;
				case 2: 	case 4: 	case 5:		default:	Set .@M$,"Safegaurd";	Break;
				case 11:	case 13:	case 14:	case 17:
				case 18:	case 19:	case 20:	case 21:	Set .@M$,"Ranged";	Break;
				case 10:	case 15:	case 23:	case 24:	Set .@M$,"Mystical";	Break;
			}
			if(.MaxMastery > getd(""+ .@M$ + "Master")){
				Setd ""+.@M$+"Exp",getd(""+.@M$+"Exp") + (strmobinfo(3,killedrid)+.wLvBonus*getiteminfo(getequipid(EQI_HAND_R),13)) * (getbattleflag("job_exp_rate") + getbattleflag("job_exp_rate") * getstatus(SC_JEXPBOOST,2));
				if(getd(""+.@M$+"Exp") > getd(""+.@M$+"Master")*(getd(""+.@M$+"Master")*.expcurve)+10+getd(""+.@M$+"Master")){
					setd ""+.@M$+"Master",getd(""+.@M$+"Master")+1;
					announce ""+.@M$+" Mastery has advanced to Lv "+getd(""+.@M$+"Master")+"!",bc_blue|bc_self;
					Setd ""+.@M$+"Exp",0;
				}
			}
		}
	}
end;
OnInit:
	bindatcmd("wi"		,"PCSTATCALC::OnWeaponInfo",1,99);
	bindatcmd("weaponinfo"	,"PCSTATCALC::OnWeaponInfo",1,99);
	bindatcmd("wlvlup"	,"PCSTATCALC::OnWeaponLevel",60,99);
	bindatcmd("wlvl"	,"PCSTATCALC::OnWeaponLevel",60,99);
	set .Weapon,1;		// Toggle Exp Groth On/Off
				//   1 = On
				//   0 = Off

	set .Left,0;		// Toggle Left Handed Mastery, left hand recives ~1/2 exp and stats
				//   1 = On
				//   0 = Off

	set .RightFist,1;	// Toggle Right fist recive Safegaurd Mastery effect
				//   1 = On
				//   0 = Off

	set .LeftFist,1;	// Toggle Left fist recive Safegaurd Mastery effect
				//   1 = On
				//   0 = Off


	set .expcurve,2500;	// Exp curve for weapon mastery
				//   Lv*Lv*.expcurve+10+Lv
				//   Default 2500

	set .MaxMastery,99;	// Sets the max Weapon Mastery Level
				//   Default 128

	set .wLvBonus,1;	// Amount of extra Weapon Exp from higher level weapons, 
				//   Bonus exp = WLv*.wLvBonus
				//   Default 1
	// System Debug
	if(.MaxMastery * .MaxMastery *(.expcurve/10) + .MaxMastery + 10 > 214748364 ){
		debugmes "[Weapon Mastery]: Max Exp Overflow Error, Lower .MaxMastery or .expcurve to fix";
	}
end;
}








